#!/usr/bin/env perl
use Mojolicious::Lite;
use Mojo::Home;
use lib 'lib';
use App::Parrot::Create::HLL;
use App::Parrot::Create::Library;

# Read configuration file
plugin 'yaml_config';
# Send file to the client
plugin 'RenderFile';

app->secret(app->config->{passphrase});

get '/' => 'index';

post '/' => sub {
    my $self = shift;

    my ($name, $builder, $harness, $parrot_revision, $with_pmc, $with_ops, $with_doc, $archive_path);
    if (defined $self->param("hll")) {
        ($name, $builder, $harness, $parrot_revision, $with_pmc, $with_ops, $with_doc)
                            = map { $self->param($_) } qw/hll_name hll_builder hll_test
                                                        hll_parrot_revision with_pmc with_ops with_doc/;
         
        my $hll = App::Parrot::Create::HLL->new();
        $hll->init($name, $builder, $harness, $parrot_revision,
                   $with_pmc, $with_ops, $with_doc, $self->app->config->{hll_template});
        
        if ($archive_path = $hll->generate()) {
            $self->app->log->debug("The HLL created!");
        }
    }
    elsif(defined $self->param("lib")){
        ($name, $builder, $harness, $parrot_revision)
                                        = map { $self->param($_) } qw/lib_name lib_builder
                                                                    lib_test lib_parrot_revision/;

        my $library = App::Parrot::Create::Library->new();
        $library->init($name, $builder, $harness, $parrot_revision, $self->app->config->{library_template});
        
        if ($archive_path = $library->generate()) {
            $self->app->log->debug("The Library created!");
        }
    }
    
    $self->render_file(
        'filepath'   => $archive_path,
        'format'     => 'zip' 
    );
} => 'index';

app->start;
